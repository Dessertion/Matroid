import Matroid.Representation.Basic

/-
We collect material on matroid invariants and closure properties of classes of matroids here.
-/

open Set

namespace Matroid

section Property

universe u

variable {Œ∑ : Type*} {P : ‚àÄ {Œ≤ : Type u}, Matroid Œ≤ ‚Üí Prop} {Œ± : Type u} {M N : Matroid Œ±}

section Invariant

/-- A matroid invariant is a parameter or property that is invariant under isomorphism. -/
class Invariant {Œ∑ : Type} (f : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Œ∑) : Prop :=
  (on_iso : ‚àÄ {Œ± Œ≤ : Type u} {M : Matroid Œ±} {N : Matroid Œ≤}, M ‚âÖ N ‚Üí f M = f N)

theorem IsIso.pred_iff_pred {P : ‚àÄ {Œ∑ : Type u}, Matroid Œ∑ ‚Üí Prop} [Invariant P]
    {Œ± Œ≤ : Type u} {M : Matroid Œ±} {N : Matroid Œ≤} (hMN : M ‚âÖ N) : P M ‚Üî P N := by
  simpa using Invariant.on_iso (f := P) hMN

theorem IsIso.pred_of_pred [Invariant P] {Œ± Œ≤ : Type u} {M : Matroid Œ±} {N : Matroid Œ≤}
  (hMN : M ‚âÖ N) (hM : P M) : P N := hMN.pred_iff_pred.1 hM

-- theorem Invariant.and {P Q : ‚àÄ {Œ∑ : Type u}, Matroid Œ∑ ‚Üí Prop} (hP : Invariant P)
--     (hQ : Invariant Q) : Invariant (fun M ‚Ü¶ P M ‚àß Q M) := by
--   intro Œ± Œ≤ M N hMN
--   simp only [eq_iff_iff]
--   rw [hP.pred_iff_pred hMN, hQ.pred_iff_pred hMN]

instance invariant_finite : Invariant.{u} Matroid.Finite where
  on_iso := by intro _ _ _ _ hMN ; rw [hMN.finite_iff]

instance invariant_finiteRk : Invariant.{u} FiniteRk where
  on_iso := by intro _ _ _ _ hMN ; rw [hMN.finiteRk_iff]

instance invariant_erk : Invariant.{u} erk where
  on_iso := by intro _ _ _ _ hMN; exact hMN.erk_eq_erk

instance invariant_fieldRep {ùîΩ : Type*} [Field ùîΩ] : Invariant.{u} (FieldRep ùîΩ) where
  on_iso := by
    intro _ _ _ _ hMN; rw [fieldRep_def, fieldRep_def, hMN.representable_iff, hMN.finite_iff]

end Invariant

section Minor

/-- A minor-closed matroid property -/
class MinorClosed (P : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Prop) : Prop :=
    (forall_minor : ‚àÄ {Œ± : Type u} {N M : Matroid Œ±}, N ‚â§m M ‚Üí P M ‚Üí P N)

theorem Minor.pred_minor [MinorClosed P] (hNM : N ‚â§m M) (hM : P M) : P N :=
  MinorClosed.forall_minor hNM hM

/-- `M` is an `ExclMinor` for property `P` if `M` is minor-minimal not satisfying `P`. -/
@[pp_dot] def ExclMinor {Œ≤ : Type u} (M : Matroid Œ≤) (P : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Prop) :=
  ¬¨ P M ‚àß ‚àÄ {N}, N <m M ‚Üí P N

theorem ExclMinor.not_prop_self (h : M.ExclMinor P) : ¬¨ P M :=
  h.1

theorem ExclMinor.prop_of_strictMinor (hM : M.ExclMinor P) (hMN : N <m M) : P N :=
  hM.2 hMN

theorem ExclMinor.eq_of_not_prop_of_minor (hM : M.ExclMinor P) (hNM : N ‚â§m M) (hN : ¬¨ P N) :
    N = M := by
  obtain (hNM' | rfl) := hNM.strictMinor_or_eq
  ¬∑ exact (hN <| hM.prop_of_strictMinor hNM').elim
  rfl

theorem ExclMinor.prop_deleteElem (hM : M.ExclMinor P) (he : e ‚àà M.E) : P (M ‚üç e) :=
  hM.prop_of_strictMinor (deleteElem_strictMinor he)

theorem ExclMinor.prop_contractElem (hM : M.ExclMinor P) (he : e ‚àà M.E) : P (M ‚üã e) :=
  hM.prop_of_strictMinor (contractElem_strictMinor he)

theorem exclMinor_iff_forall_contract_delete [MinorClosed P] {M : Matroid Œ±} :
    M.ExclMinor P ‚Üî ¬¨ P M ‚àß ‚àÄ e ‚àà M.E, P (M ‚üã e) ‚àß P (M ‚üç e) := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®h.not_prop_self, fun e he ‚Ü¶ ‚ü®h.prop_contractElem he, h.prop_deleteElem he‚ü©‚ü©,
    fun h ‚Ü¶ ‚ü®h.1, fun {N} hNM ‚Ü¶ ?_‚ü©‚ü©
  obtain ‚ü®e, he, (hc | hd)‚ü© := strictMinor_iff_minor_contract_or_delete.1 hNM
  ¬∑ exact hc.pred_minor (h.2 e he).1
  exact hd.pred_minor (h.2 e he).2

theorem mem_iff_not_exists_exclMinor_minor [MinorClosed P] (M : Matroid Œ±) [M.Finite] :
    P M ‚Üî ¬¨ ‚àÉ N, N ‚â§m M ‚àß N.ExclMinor P := by
  refine ‚ü®fun h ‚ü®N, hNM, hN‚ü© ‚Ü¶ hN.not_prop_self <| hNM.pred_minor h,
    fun h ‚Ü¶ by_contra fun hM ‚Ü¶ h ?_‚ü©
  obtain (hM' | hM') := em (M.ExclMinor P)
  ¬∑ exact ‚ü®M, Minor.refl M, hM'‚ü©
  simp_rw [ExclMinor, not_and, not_forall] at hM'
  obtain ‚ü®N, hNM, hPN‚ü© := hM' hM
  have := hNM.encard_ground_lt
  have hNfin := hNM.minor.finite
  have h' := mt (mem_iff_not_exists_exclMinor_minor (M := N)).2 hPN
  rw [not_not] at h'
  obtain ‚ü®N', hN'N, hN'‚ü© := h'
  exact ‚ü®N', hN'N.trans hNM.minor, hN'‚ü©
termination_by _ => M.E.encard

instance minorClosed_finite : MinorClosed.{u} Matroid.Finite where
  forall_minor := fun a _ ‚Ü¶ Minor.finite a

instance minorClosed_finiteRk : MinorClosed.{u} FiniteRk where
  forall_minor := fun a _ ‚Ü¶ Minor.finiteRk a

instance minorClosed_finitary : MinorClosed.{u} Finitary where
  forall_minor := fun a _ ‚Ü¶ Minor.finitary a

instance minorClosed_fieldRep (ùîΩ : Type*) [Field ùîΩ] :
    MinorClosed (FieldRep ùîΩ) :=
  ‚ü®fun {_ _ _} hNM ‚ü®hMrep, hMfin‚ü© ‚Ü¶ ‚ü®hMrep.minor hNM, hNM.pred_minor hMfin‚ü©‚ü©

end Minor

section Dual

/-- A self-dual matroid parameter -/
def SelfDual (P : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Œ∑) : Prop :=
  ‚àÄ {Œ± : Type u} (M : Matroid Œ±), P M = P MÔπ°

/-- A matroid property that is preserved under taking duals. -/
class DualClosed (P : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Prop) : Prop :=
  (forall_dual : ‚àÄ {Œ± : Type u} {M : Matroid Œ±}, P M ‚Üí P MÔπ°)

theorem toDualPred [DualClosed P] (hM : P M) : P MÔπ° :=
  DualClosed.forall_dual hM

theorem ofDualPred [DualClosed P] (hM : P MÔπ°) : P M :=
  M.dual_dual ‚ñ∏ toDualPred hM

@[simp] theorem iffDualPred [DualClosed P] : P MÔπ° ‚Üî P M :=
  ‚ü®ofDualPred, toDualPred‚ü©

/-- The class of finite matroids is closed under duality -/
instance dualClosed_finite : DualClosed.{u} Matroid.Finite where
  forall_dual := fun a ‚Ü¶ by infer_instance

/-- The class of finite `ùîΩ`-representable matroids is closed under duality -/
instance dualClosed_fieldRep (ùîΩ : Type*) [Field ùîΩ] : DualClosed.{u} (FieldRep ùîΩ) where
  forall_dual := fun {_ _} ‚ü®hMrep, hMfin‚ü© ‚Ü¶ ‚ü®hMrep.dual, by infer_instance‚ü©

theorem ExclMinor.toDual [DualClosed P] (h : M.ExclMinor P) : MÔπ°.ExclMinor P :=
  ‚ü®fun h' ‚Ü¶ h.1 <| ofDualPred h',
    fun {_} hNM ‚Ü¶ ofDualPred (h.prop_of_strictMinor <| strictMinor_dual_iff_dual_strictMinor.1 hNM)‚ü©

end Dual

section Simple

/-- A matroid property `P` is `RemoveLoopClosed` if `P M ‚Üî P M.removeLoops` for all `M`. -/
class RemoveLoopClosed (P : ‚àÄ {Œ≤ : Type u}, Matroid Œ≤ ‚Üí Prop) : Prop :=
  /- `P` holds for `M` iff it holds after removing loops. -/
  (iff_removeLoops : ‚àÄ {Œ± : Type u} {M : Matroid Œ±}, P M ‚Üî P M.removeLoops)

@[simp] theorem pred_removeLoops_iff [RemoveLoopClosed P] {M : Matroid Œ±} : P M.removeLoops ‚Üî P M :=
  RemoveLoopClosed.iff_removeLoops.symm

theorem removeLoopClosed_iff_forall_delete :
    RemoveLoopClosed P ‚Üî
      ‚àÄ {Œ± : Type u} {M : Matroid Œ±} {X : Set Œ±}, X ‚äÜ M.cl ‚àÖ ‚Üí (P M ‚Üî P (M ‚üç X)) := by
  refine ‚ü®fun h {Œ±} M X hX ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®fun {Œ≥ M} ‚Ü¶ ?_‚ü© ‚ü©
  ¬∑ rw [h.iff_removeLoops, Iff.comm, h.iff_removeLoops, removeLoops_eq_delete,
      removeLoops_eq_delete, delete_cl_eq, empty_diff, delete_delete, union_diff_self,
      union_eq_self_of_subset_left hX]
  simp only [removeLoops_eq_delete]
  exact h Subset.rfl

/-- In the setting of finite matroids, there would be an `iff` version of this lemma.  -/
theorem RemoveLoopClosed.iff_delete_loop (hP : RemoveLoopClosed P) {M : Matroid Œ±} (he : M.Loop e) :
    P M ‚Üî P (M ‚üç e) := by
  rw [removeLoopClosed_iff_forall_delete] at hP
  apply hP
  simpa

theorem ExclMinor.loopless [RemoveLoopClosed P] [MinorClosed P] (hM : M.ExclMinor P) :
    M.Loopless := by
  rw [‚Üê removeLoops_eq_self_iff]
  apply hM.eq_of_not_prop_of_minor M.removeLoops_restriction.minor
  simp_rw [pred_removeLoops_iff]
  exact hM.not_prop_self

/-- A matroid property `P` is `SimpClosed` if `P M ‚Üî P M.simplification` for all `M`. -/
class SimpClosed (P : ‚àÄ {Œ± : Type u}, Matroid Œ± ‚Üí Prop) : Prop :=
  /- `P` holds for `M` iff it holds after simplifying. -/
  (iff_simp : ‚àÄ {Œ≤ : Type u} {M : Matroid Œ≤}, P M ‚Üî P M.simplification)

@[simp] theorem pred_simplification_iff (P : ‚àÄ {Œ≤ : Type u}, Matroid Œ≤ ‚Üí Prop) [SimpClosed P] :
    P M.simplification ‚Üî P M :=
  SimpClosed.iff_simp.symm

instance removeLoopClosed_of_simpClosed (P : ‚àÄ {Œ≤ : Type u}, Matroid Œ≤ ‚Üí Prop) [SimpClosed P] :
    RemoveLoopClosed P where
  iff_removeLoops := fun {Œ±} M ‚Ü¶ by
    rw [‚Üê pred_simplification_iff P, Iff.comm, ‚Üê pred_simplification_iff P,
      removeLoops_simplification_eq]

theorem ExclMinor.simple [SimpClosed P] [MinorClosed P] (hM : M.ExclMinor P) : M.Simple := by
  rw [‚Üê simplification_eq_self_iff]
  apply hM.eq_of_not_prop_of_minor (simplification_restriction M).minor
  simp_rw [pred_simplification_iff]
  exact hM.not_prop_self

end Simple

section Finite

/-- The property of a matroid class that all its members are finite -/
class FinClass (P : ‚àÄ {Œ≤ : Type u}, Matroid Œ≤ ‚Üí Prop) : Prop where
  (forall_fin : ‚àÄ {Œ± : Type u} {M : Matroid Œ±}, P M ‚Üí M.Finite)

theorem finite_of_pred [FinClass P] (hM : P M) : M.Finite :=
    FinClass.forall_fin hM

instance fieldRep_finClass (ùîΩ : Type*) [Field ùîΩ] : FinClass.{u} (FieldRep ùîΩ) where
  forall_fin := fun h ‚Ü¶ h.2

instance finite_finClass : FinClass.{u} Matroid.Finite where
  forall_fin := id

theorem ExclMinor.finite [FinClass P] (hM : M.ExclMinor P) : M.Finite := by
  obtain (rfl | ‚ü®‚ü®e,he‚ü©‚ü© ) := eq_emptyOn_or_nonempty M
  ¬∑ infer_instance
  have := finite_of_pred <| hM.prop_deleteElem he
  exact ‚ü®((M ‚üç e).ground_finite.insert e).subset (by simp)‚ü©

section Finite
